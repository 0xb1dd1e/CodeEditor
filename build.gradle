
version = '0.2.0'

buildscript {
	repositories {
		ivy { url '../repository' }
		ivy { url 'http://unity-technologies.github.com/kaizen/repositories/unstable' }
		mavenCentral()
	}
	dependencies {
		classpath 'kaizen:kaizen:0.3.0'
	}
}

allprojects {
	apply plugin: 'vs2010'
}

/*
project(':CodeEditor.Text.UI.Unity.Editor') {
	vs {
		project {
			postBuildEvent {
				task ':updateUnityProject'
			}
		}
	}
}
*/

vs {
	solutions {

		def frameworkProjects = [
			':CodeEditor.Collections',
			':CodeEditor.Composition',
			':CodeEditor.IO',
			':CodeEditor.Testing',
			':CodeEditor.Composition.Client',
			':CodeEditor.Composition.Server'
		]
		def isTestProject = { p -> (p.name =~ /\.Tests(\.Fixtures)?/).any() }
		def testProjects = subprojects.findAll(isTestProject) .collect { it.path }
		def languageProjects = subprojects.findAll { it.name.contains('Languages') && !(it.path in testProjects) }.collect { it.path }
		def categorizedProjects = (frameworkProjects + testProjects + languageProjects).toSet()
		def remainingProjects = subprojects.collect { it.path }.findAll { !(it in categorizedProjects) }

		CodeEditor {
			folder('Frameworks') { f ->
				frameworkProjects.each {
					f.project(it)
				}
			}
			folder('Languages') { f ->
				languageProjects.each {
					f.project(it)
				}
			}
			folder('Tests') { f ->
				testProjects.each {
					f.project(it)
				}
			}
			remainingProjects.each {
				project(it)
			}
		}
	}
}

allprojects {
	repositories {
		ivy { url 'http://unity-technologies.github.com/kaizen/repositories/unstable' }
	}
	apply plugin: 'nuget'
}

configurations {
	editor {
		description "Configuration for editor extension artifacts."
	}
	tests {
		description "Configuration for testing artifacts."
	}
	server {
		description "Configuration for server artifacts."
	}
}

apply plugin: 'assembly-bundle'
apply plugin: 'nunit'
apply plugin: 'idea'

def bundleDependency(configuration, subProject) {
	dependencies.add(configuration, dependencies.project(path: subProject.path))
}

subprojects { subProject ->
	switch (subProject.name) {
	case ~/.+\.Tests$/:
		apply plugin: 'nunit-assembly'
		dependencies {
			// Foo.Bar.Tests depends on Foo.Bar
			'default' project(subProject.path[0..-('.Tests'.length() + 1)])
		}
		bundleDependency('tests', subProject)
		break
	case ~/.+\.Server$/:
		bundleDependency('server', subProject)
		break
	case ~/.+\.(Testing|Fixtures)$/:
		break
	default:
		bundleDependency('editor', subProject)
	}
}

task publish {
	dependsOn uploadEditor
	project.configurations.editor.dependencies.each {
		dependsOn ":$it.name:uploadDefault" 
	}
}

class UpdateDir extends DefaultTask {

	def destinationDir

	def setDestinationDir(destinationDir) {
		this.destinationDir = project.file(destinationDir)
		outputs.dir this.destinationDir
	}

	@TaskAction
	def update() {
		logger.info("Updating $destinationDir")
		def destDir = destinationDir
		inputs.sourceFiles.each { f ->
			project.copy {
				from project.zipTree(f)
				into destDir
				include '*.dll'
				exclude 'UnityE*'
			}
		}
	}
}

def unityProjectDir = project.properties.get('unityProjectDir', 'UnityProject')

task updateUnityProjectEditorConfig(type: UpdateDir) {
	description 'Builds and copies required assemblies to the configured Unity project (unityProjectDir)'
	inputs.source project.configurations.editor
	destinationDir = "$unityProjectDir/Assets/kaizen/lib/Editor"
}

task updateUnityProjectServerConfig(type: UpdateDir) {
	description 'Builds and copies required assemblies to the configured Unity project (unityProjectDir)'
	inputs.source project.configurations.server
	destinationDir = "$unityProjectDir/Library/CodeEditor/Server"
}

task updateUnityProject {
	dependsOn updateUnityProjectEditorConfig
	dependsOn updateUnityProjectServerConfig
}


task open {
	dependsOn updateUnityProject
	doFirst {
		project.exec {
			executable rootProject.extensions.unity.executable
			args '-projectPath'
			args file(unityProjectDir)
		}
	}
}

def rxDependency(rxPackage, rxAssembly, rxConfiguration) {
	['group': rxPackage, 'name': rxAssembly, 'version': '1.0.2856.0', 'configuration': rxConfiguration]
}

ext {
	rxCore = { assemblyName, configuration = 'Net35' -> rxDependency('Rx-Core-old', assemblyName, configuration) }
	rxMain = { assemblyName, configuration = 'Net35' -> rxDependency('Rx-Main', assemblyName, configuration) }
	references = [
		'nunitFramework': "nunit:nunit.framework:${nunit.version}",
		'moq': 'Moq:Moq:4.0+',
		'adt': 'boo-extensions:Boo.Adt:0.1.1+',
		'ometa': 'boo-extensions:Boo.OMeta:0.1.1+',
		'rx35': [
			rxMain('System.Reactive'),
			rxCore('System.Observable'),
			rxCore('System.CoreEx'),
			rxCore('System.Threading'),
		],
		'rx40': [
			rxMain('System.Reactive', 'Net4'),
			rxCore('System.CoreEx', 'Net4'),
		]
	]
}

